message(STATUS "Morpheus-Oracle Tests are enabled")

morpheusoracle_include_directories(${CMAKE_CURRENT_BINARY_DIR})
morpheusoracle_include_directories(${CMAKE_CURRENT_SOURCE_DIR})
morpheusoracle_include_directories(
  ${MorpheusOracle_SOURCE_DIR}/tests/category_files)

foreach(Tag Serial;OpenMP;Cuda;HIP)
  string(TOUPPER ${Tag} DEVICE)
  string(TOLOWER ${Tag} dir)
  global_set(${Tag}_SOURCES)

  if(Morpheus_ENABLE_${DEVICE})
    set(testdir ${CMAKE_CURRENT_BINARY_DIR}/${dir})
    file(MAKE_DIRECTORY ${testdir})

    foreach(Name RunFirstTuner TuneRunFirst TypeTraits)
      set(file ${testdir}/Test_${Tag}_${Name}.cpp)
      # Write to a temporary intermediate file and call configure_file to avoid
      # updating timestamps triggering unnecessary rebuilds on subsequent cmake
      # runs.
      file(WRITE ${testdir}/dummy.cpp "#include <Test${Tag}_Category.hpp>\n"
                                      "#include <Test_${Name}.hpp>\n")
      configure_file(${testdir}/dummy.cpp ${file})
      global_append(${Tag}_SOURCES ${file})
    endforeach()
  endif()
endforeach()

set(ALL_SOURCES)

if(Morpheus_ENABLE_SERIAL)
  if(MorpheusOracle_ENABLE_INDIVIDUAL_TESTS)
    morpheusoracle_add_executable_and_test(
      UnitTest_Serial SOURCES TestMain.cpp ${Serial_SOURCES} TESTONLYLIBS
      morpheusoracle_gtest)
  endif()
  list(APPEND ALL_SOURCES ${Serial_SOURCES})
endif()

if(Morpheus_ENABLE_OPENMP)
  if(MorpheusOracle_ENABLE_INDIVIDUAL_TESTS)
    morpheusoracle_add_executable_and_test(
      UnitTest_OpenMP SOURCES TestMain.cpp ${OpenMP_SOURCES} TESTONLYLIBS
      morpheusoracle_gtest)
  endif()
  list(APPEND ALL_SOURCES ${OpenMP_SOURCES})
endif()

if(Morpheus_ENABLE_CUDA)
  if(MorpheusOracle_ENABLE_INDIVIDUAL_TESTS)
    morpheusoracle_add_executable_and_test(
      UnitTest_Cuda SOURCES TestMain.cpp ${Cuda_SOURCES} TESTONLYLIBS
      morpheusoracle_gtest)
  endif()
  list(APPEND ALL_SOURCES ${Cuda_SOURCES})
endif()

if(Morpheus_ENABLE_HIP)
  if(MorpheusOracle_ENABLE_INDIVIDUAL_TESTS)
    morpheusoracle_add_executable_and_test(
      UnitTest_HIP SOURCES TestMain.cpp ${HIP_SOURCES} TESTONLYLIBS
      morpheusoracle_gtest)
  endif()
  list(APPEND ALL_SOURCES ${HIP_SOURCES})
endif()

if(NOT MorpheusOracle_ENABLE_INDIVIDUAL_TESTS)
  morpheusoracle_add_executable_and_test(
    UnitTest SOURCES TestMain.cpp ${ALL_SOURCES} TESTONLYLIBS
    morpheusoracle_gtest)
endif()
